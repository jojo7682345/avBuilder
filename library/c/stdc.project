inherit compiler = "gcc";
inherit linker = compiler;
inherit archiver = "ar";
inherit validCextensions = [ ".c", ".C", ".s", ".S" ];

compile(sources, outDir, flags, includes){
    var output[arraySize(sources)];
    var retCodes[arraySize(sources)];
    var failed;
    foreach sourceFile[index] from sources perform {
        command : retCodes[index] {
            args = [
                "$flags",
                "-I*includes"
            ];
            objectFile = outDir + "/" + fileBaseName(sourceFile) + ".o";
            makeDirs(filePath(objectFile));
            command = "$compiler -c $sourceFile $args -o $objectFile";
            output[index]=objectFile;
        };
        if(retCodes[index]!=0){
            print("compiling ");
            print(sourceFile);
            println(" failed!");
            failed = 1;
        }
    };
    if(failed){
        return [];
    }
    return output;
}

archiveLibrary(outDir, libraryName, flags, objects){
    var output;
    var retCode;
    perform command : retCode {
        makeDirs(outDir);
        command = "$archiver $flags -o $outDir/$libraryName *objects";
        output=outDir + "/" + libraryName;
    }
    if(retCode){
        perform{
            print("linking ");
            print(libraryName);
            println(" failed!");
        }
        return [];
    }
    return output;
}

linkExecutable(objects, outDir, libraryName, flags, libDirs, libs){
    var output;
    var retCode;
    perform {
        command : retCode {
            outputExecutable = outDir + "/" + libraryName;
            command = "$linker $flags *objects -L*libDirs -l*libs -o $outputExecutable";
            output = libraryName;
        }
    }
    
    if(retCode!=0) {
        perform{
            print("linking ");
            print(libraryName);
            println(" failed!");
        }
        return [];
    }
    return output;
}

buildExecutable(sources, buildDir, compileFlags, linkFlags, includes, libDirs, libs, executableName, outDir){
    var executable;
    var failed;
    perform{
        objects = compile(sources, buildDir, compileFlags, includes);
        if(arraySize(objects)!=0){
            executable = linkExecutable(objects, outDir, executableName, linkFlags, libDirs, libs);
            if(arraySize(executable)!=0){ 
                print("Successfully build ");
                println(executableName);
            }else{
                print("Building ");print(executableName);println(" failed");
            }
        }else{
            executable = [];
            print("Building ");print(executableName);println(" failed");
        }
    }
    return executable;
}

buildStaticLibrary(sources, buildDir, compileFlags, archiveFlags, includes, additionalObjects, libraryName, outDir){
    var library;
    var objects;
    perform {
        objects = compile(sources, buildDir, compileFlags, includes);
    }
    if(arraySize(objects)==0){
        perform{
            print("compilation failed");
        }
        return [];
    }
    var totalObjects[arraySize(objects) + arraySize(additionalObjects)];
    foreach object[index] from objects perform {
        totalObjects[index] = object;
    }
    foreach additionalObject[index] from additionalObjects perform {
        totalObjects[index + arraySize(objects)] = additionalObject;
    }
    perform {
        library = archiveLibrary(outDir, "lib" + libraryName + ".a", archiveFlags, totalObjects);
    }
    if(library){
        return library;
    }
    perform{
        print("builing static library ");
        print(libraryName);
        println(" failed!");
    }
    return [];
}

getSourceFiles(dir){
    return filter(FILTER_TYPE_ENDS_WITH, validCextensions, files in dir recursive);
}
